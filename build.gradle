buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'com.gradle.plugin-publish' version '1.2.1'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation gradleApi()
    implementation localGroovy()
    implementation 'nu.studer:java-ordered-properties:1.0.4'

    testImplementation gradleTestKit()
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}

ext {
    // Author
    developerId = 'davideas'
    developerName = 'Davide Steduto'
    developerEmail = 'dave.dna@gmail.com'

    // Library Repository
    groupId = 'eu.davidea'
    artifactId = 'grabver'
    libraryName = 'GrabVer'
    libraryVersion = '2.0.3'
    libraryTitle = 'Gradle Automatic Build Versioning Plugin'
    libraryDescription = 'An easy Gradle plugin that follows semver.org rules to automatically generate the Patch version, Build number and Code version, while Major, Minor and Pre-Release suffix remain under our control.'
    libraryTags = ['semver', 'version', 'versioning', 'build-versioning', 'automatic-versioning', 'intellij-idea', 'android-studio', 'auto-reset']
    siteUrl = 'https://github.com/davideas/GrabVer'
    gitUrl = 'https://github.com/davideas/GrabVer.git'

    // License
    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ['Apache-2.0']
}

group groupId
version libraryVersion

javadoc {
    // Suppresses the warnings
    options.addBooleanOption('Xdoclint:none', true)
}

// To publish to Maven local: gradle publishToMavenLocal
// To publish to Gradle Plugin Central: gradle publishPlugin [--validate-only]
gradlePlugin {
    website = project.siteUrl
    vcsUrl = project.gitUrl

    plugins {
        grabverPlugin {
            id = project.groupId + '.' + project.artifactId
            displayName = project.libraryTitle
            description = project.libraryDescription
            tags = project.libraryTags
            implementationClass = project.groupId + '.gradle.GrabVer'
        }
    }
}

// To publish to Maven: gradle publish
if (gradle.getStartParameter().getTaskNames().contains("publish")) {
    //apply from: 'maven-publish.gradle'
}

// To run unit tests add in command line: -P test
tasks.test {
    it.jvmArgs = ["--add-opens=java.base/java.lang=ALL-UNNAMED"]

    onlyIf {
        providers.gradleProperty("test").isPresent()
    }
}
